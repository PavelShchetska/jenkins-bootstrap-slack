//gradle plugins
apply plugin: 'maven'

//default
defaultTasks 'getjenkins', 'getplugins'

//perhaps use JENKINS_HOME some day
//avoiding JENKINS_HOME because I only want to copy if file doesn't exist
//by default the copy function will overwrite plugins in JENKINS_HOME if the hash is different
ext.jenkinsHome = System.getenv('JENKINS_HOME')?:'../my_jenkins_home'
ext.bootstrapHome = System.getenv('BOOTSTRAP_HOME')?:'.'
ext.pluginsDest = bootstrapHome
ext.jenkinsDest = bootstrapHome

task cleanJenkins(type: Delete) {
    doFirst {
        //stop jenkins just in case it's running
        def stdout = new StringBuilder()
        def stderr = new StringBuilder()
        def process = ["${bootstrapHome}/scripts/provision_jenkins.sh", 'clean'].execute()
        process.waitForProcessOutput(stdout, stderr)
        println stdout.toString()
        println stderr.toString()
    }
    delete '.gradle'
    delete 'jenkins-cli.jar'
    delete 'jenkins.war'
    delete 'plugins'
}

clean.dependsOn cleanJenkins

configurations {
    getplugins {
        description = 'Download Jenkins plugins .jpi files.'
        transitive = false
    }
    getjenkins {
        description = 'Download Jenkins WAR file.'
        transitive = false
    }
}
repositories {
    maven {
        name 'jenkins'
        delegate.url('http://repo.jenkins-ci.org/public/')
    }
}
dependencies {
    //get Jenkins
    getjenkins 'org.jenkins-ci.main:jenkins-war:2.46.1@war'

    //get plugins
    getplugins 'com.coravy.hudson.plugins.github:github:1.27.0@hpi'
    getplugins 'org.jenkins-ci.main:maven-plugin:2.15.1@hpi'
    getplugins 'org.jenkins-ci.plugins.icon-shim:icon-shim:2.0.3@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-scm-step:2.4@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.9@hpi'
    getplugins 'org.jenkins-ci.plugins:ant:1.4@hpi'
    getplugins 'org.jenkins-ci.plugins:antisamy-markup-formatter:1.5@hpi'
    getplugins 'org.jenkins-ci.plugins:bouncycastle-api:2.16.1@hpi'
    getplugins 'org.jenkins-ci.plugins:cobertura:1.9.8@hpi'
    getplugins 'org.jenkins-ci.plugins:covcomplplot:1.1.1@hpi'
    getplugins 'org.jenkins-ci.plugins:credentials:2.1.13@hpi'
    getplugins 'org.jenkins-ci.plugins:cvs:2.13@hpi'
    getplugins 'org.jenkins-ci.plugins:display-url-api:2.0@hpi'
    getplugins 'org.jenkins-ci.plugins:external-monitor-job:1.7@hpi'
    getplugins 'org.jenkins-ci.plugins:ghprb:1.36.2@hpi'
    getplugins 'org.jenkins-ci.plugins:git-client:2.4.2@hpi'
    getplugins 'org.jenkins-ci.plugins:git:3.3.0@hpi'
    getplugins 'org.jenkins-ci.plugins:github-api:1.85@hpi'
    getplugins 'org.jenkins-ci.plugins:github-oauth:0.26@hpi'
    getplugins 'org.jenkins-ci.plugins:groovy:2.0@hpi'
    getplugins 'org.jenkins-ci.plugins:htmlpublisher:1.13@hpi'
    getplugins 'org.jenkins-ci.plugins:javadoc:1.4@hpi'
    getplugins 'org.jenkins-ci.plugins:junit:1.20@hpi'
    getplugins 'org.jenkins-ci.plugins:ldap:1.14@hpi'
    getplugins 'org.jenkins-ci.plugins:mailer:1.20@hpi'
    getplugins 'org.jenkins-ci.plugins:mapdb-api:1.0.9.0@hpi'
    getplugins 'org.jenkins-ci.plugins:matrix-auth:1.5@hpi'
    getplugins 'org.jenkins-ci.plugins:matrix-project:1.10@hpi'
    getplugins 'org.jenkins-ci.plugins:pam-auth:1.3@hpi'
    getplugins 'org.jenkins-ci.plugins:plain-credentials:1.4@hpi'
    getplugins 'org.jenkins-ci.plugins:scm-api:2.1.1@hpi'
    getplugins 'org.jenkins-ci.plugins:script-security:1.27@hpi'
    getplugins 'org.jenkins-ci.plugins:slack:2.2@hpi'
    getplugins 'org.jenkins-ci.plugins:ssh-agent:1.15@hpi'
    getplugins 'org.jenkins-ci.plugins:ssh-credentials:1.13@hpi'
    getplugins 'org.jenkins-ci.plugins:ssh-slaves:1.17@hpi'
    getplugins 'org.jenkins-ci.plugins:structs:1.6@hpi'
    getplugins 'org.jenkins-ci.plugins:subversion:2.7.2@hpi'
    getplugins 'org.jenkins-ci.plugins:token-macro:2.1@hpi'
    getplugins 'org.jenkins-ci.plugins:translation:1.15@hpi'
    getplugins 'org.jenkins-ci.plugins:windows-slaves:1.3.1@hpi'

    //additional pipeline plugins
    getplugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-rest-api:2.6@hpi'
    getplugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-stage-view:2.6@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.5@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-api:2.13@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-basic-steps:2.4@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.7@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-cps:2.29@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-durable-task-step:2.10@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-job:2.10@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-multibranch:2.14@hpi'
    getplugins 'org.jenkins-ci.plugins.workflow:workflow-support:2.14@hpi'
    getplugins 'org.jenkins-ci.plugins:authentication-tokens:1.3@hpi'
    getplugins 'org.jenkins-ci.plugins:branch-api:2.0.8@hpi'
    getplugins 'org.jenkins-ci.plugins:cloudbees-folder:6.0.3@hpi'
    getplugins 'org.jenkins-ci.plugins:credentials-binding:1.11@hpi'
    getplugins 'org.jenkins-ci.plugins:docker-commons:1.6@hpi'
    getplugins 'org.jenkins-ci.plugins:docker-workflow:1.10@hpi'
    getplugins 'org.jenkins-ci.plugins:durable-task:1.13@hpi'
    getplugins 'org.jenkins-ci.plugins:git-server:1.7@hpi'
    getplugins 'org.jenkins-ci.plugins:github-branch-source:2.0.5@hpi'
    getplugins 'org.jenkins-ci.plugins:pipeline-build-step:2.5@hpi'
    getplugins 'org.jenkins-ci.plugins:pipeline-graph-analysis:1.3@hpi'
    getplugins 'org.jenkins-ci.plugins:pipeline-input-step:2.5@hpi'
    getplugins 'org.jenkins-ci.plugins:pipeline-milestone-step:1.3.1@hpi'
    getplugins 'org.jenkins-ci.plugins:pipeline-stage-step:2.2@hpi'
    getplugins 'org.jenkins-ci.ui:ace-editor:1.1@hpi'
    getplugins 'org.jenkins-ci.ui:handlebars:1.1.1@hpi'
    getplugins 'org.jenkins-ci.ui:jquery-detached:1.2.1@hpi'
    getplugins 'org.jenkins-ci.ui:momentjs:1.1.1@hpi'
    getplugins 'org.jenkinsci.plugins:pipeline-model-api:1.1.3@hpi'
    getplugins 'org.jenkinsci.plugins:pipeline-model-declarative-agent:1.1.1@hpi'
    getplugins 'org.jenkinsci.plugins:pipeline-model-definition:1.1.3@hpi'
    getplugins 'org.jenkinsci.plugins:pipeline-model-extensions:1.1.3@hpi'
    getplugins 'org.jenkinsci.plugins:pipeline-stage-tags-metadata:1.1.3@hpi'
}

//download and copy specific versions of plugins
task getplugins(type: Copy) {
    into "${pluginsDest}/plugins"
    from configurations.getplugins
    include '*.hpi'
    rename '(.*)-[.0-9]+.hpi$', '$1.jpi'
}

//download and copy a specific version of Jenkins
task getjenkins(type: Copy) {
    into jenkinsDest
    from configurations.getjenkins
    include '*.war'
    rename '.*', 'jenkins.war'
}

//http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
//generate gradlew with: gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
